#Использовать ".."
#Использовать cmdline
#Использовать ibcmdrunner
#Использовать asserts

Перем Парсер, ИнформацияОПараметрахПоУмолчанию; // BSLLS:MissingVariablesDescription-off

Функция Конструктор()
	ШаблонСхемы = Новый Соответствие;

	ШаблонСхемы.Вставить("$schema", "http://json-schema.org/draft-07/schema");
	ШаблонСхемы.Вставить("id", "https://github.com/vanessa-opensource/vanessa-runner");
	ШаблонСхемы.Вставить("title", "Vanessa-runner settings json-schema");
	ШаблонСхемы.Вставить("description", "Схема json-файла c настройками для Vanessa-runner");
	ШаблонСхемы.Вставить("type", "object");
	ШаблонСхемы.Вставить("additionalProperties", Ложь);
	ШаблонСхемы.Вставить("properties", Новый Соответствие);

	Возврат ШаблонСхемы;
КонецФункции

Процедура НачальнаяИнициализацияПроекта()
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ПараметрыСистемы.ЭтоWindows = Найти(ВРег(СистемнаяИнформация.ВерсияОС), "WINDOWS") > 0;	
	МенеджерКомандПриложения.РегистраторКоманд(ПараметрыСистемы);
	Парсер = Новый ПарсерАргументовКоманднойСтроки();
	МенеджерКомандПриложения.ЗарегистрироватьКоманды(Парсер);
КонецПроцедуры

Функция ИнформацияПоКомандам()
	
	ИнформацияПоКомандам = Новый Соответствие;

	СписокКоманд = Парсер.СправкаВозможныеКоманды();
	ИнформацияОПараметрахПоУмолчанию = Новый Соответствие;
	Для каждого ИнформацияОКоманде Из СписокКоманд Цикл
		СхемаКоманды = Новый Соответствие;
		СхемаКоманды.Вставить("id", СтрШаблон("/properties/%1", Нрег(ИнформацияОКоманде.Команда)));
		СхемаКоманды.Вставить("description", ИнформацияОКоманде.Пояснение);
		СхемаКоманды.Вставить("type", "object");
		СхемаКоманды.Вставить("additionalProperties", Ложь);
		СхемаКоманды.Вставить("properties", ИнформацияПоПараметрамКоманды(ИнформацияОКоманде)); 
		
		ИнформацияПоКомандам.Вставить(ИнформацияОКоманде.Команда, СхемаКоманды);
		
	КонецЦикла;
	ИнформацияПоКомандам.Вставить("default", ИнформацияОПараметрахПоУмолчанию);
	// BSLLS-off
	ИнформацияПоКомандам.Вставить("$schema", Новый Структура("default, description, id, type",
																"vanessa-runner-schema.json",
																"Path of vanessa-runner-schema.json.", 
																"/properties/$schema",
																"string"));
	// BSLLS-on
	Возврат ИнформацияПоКомандам;
КонецФункции

Функция ИнформацияПоПараметрамКоманды(ИнформацияОКоманде)
	ПараметрыКоманды = Новый Соответствие;
	Для каждого Параметр Из ИнформацияОКоманде.Параметры Цикл											
		ПараметрыКоманды.Вставить(Параметр.Имя, СтруктураСвойствПараметра(Параметр, ИнформацияОКоманде.Команда));
		Если ЭтоПараметрПоУмолчанию(Параметр.Имя) Тогда
			ИнформацияОПараметрахПоУмолчанию.Вставить(Параметр.Имя, СтруктураСвойствПараметра(Параметр, "default"));
		КонецЕсли;
	КонецЦикла;

	Возврат ПараметрыКоманды;
КонецФункции

Функция СтруктураСвойствПараметра(Параметр, ИмяКоманды)
	СтруктураСвойствПараметра = Новый Структура;
	СтруктураСвойствПараметра.Вставить("description", Параметр.Пояснение);
	СтруктураСвойствПараметра.Вставить("id", СтрШаблон("/properties/%1/%2",  // BSLLS:NestedFunctionInParameters-off
												НРег(ИмяКоманды),
												НРег(Параметр.Имя)));
	СтруктураСвойствПараметра.Вставить("type", ?(Параметр.ЭтоФлаг, "boolean", "string"));
	СтруктураСвойствПараметра.Вставить("default", ?(Параметр.ЭтоФлаг, Ложь, ""));
	СтруктураСвойствПараметра.Вставить("title", СтрШаблон("The %1 Schema", Параметр.Имя));

	Возврат СтруктураСвойствПараметра;	
КонецФункции

Функция ЭтоПараметрПоУмолчанию(ИмяПараметра)
	МассивПараметровПоУмолчанию = Новый Массив;
	МассивПараметровПоУмолчанию.Добавить("--db-pwd");
	МассивПараметровПоУмолчанию.Добавить("--db-user");
	МассивПараметровПоУмолчанию.Добавить("--ibconnection");
	МассивПараметровПоУмолчанию.Добавить("--ibcmd");
	МассивПараметровПоУмолчанию.Добавить("--dbms-pwd");
	МассивПараметровПоУмолчанию.Добавить("--dbms-user");
	МассивПараметровПоУмолчанию.Добавить("--dbms-type");
	МассивПараметровПоУмолчанию.Добавить("--dbms-base");
	МассивПараметровПоУмолчанию.Добавить("--dbms-server");
	МассивПараметровПоУмолчанию.Добавить("--ordinaryapp");
	МассивПараметровПоУмолчанию.Добавить("--root");
	МассивПараметровПоУмолчанию.Добавить("--v8version");
	МассивПараметровПоУмолчанию.Добавить("--locale");
	МассивПараметровПоУмолчанию.Добавить("--language");
	МассивПараметровПоУмолчанию.Добавить("--uccode");
	МассивПараметровПоУмолчанию.Добавить("--settings");
	МассивПараметровПоУмолчанию.Добавить("--debuglogfile");
	МассивПараметровПоУмолчанию.Добавить("--debuglog");
	МассивПараметровПоУмолчанию.Добавить("--nocacheuse");
	МассивПараметровПоУмолчанию.Добавить("--additional");
	МассивПараметровПоУмолчанию.Добавить("--workspace");

	Возврат Не МассивПараметровПоУмолчанию.Найти(ИмяПараметра) = Неопределено;
КонецФункции

НачальнаяИнициализацияПроекта();

Схема = Конструктор();

Схема.Вставить("properties", ИнформацияПоКомандам());

ЗаписьJSON = Новый ЗаписьJSON;
ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Авто, Символы.Таб);
ЗаписьJSON.ОткрытьФайл("vanessa-runner-schema.json",,Ложь,ПараметрыЗаписиJSON);
ЗаписатьJSON(ЗаписьJSON, Схема);
ЗаписьJSON.Закрыть();